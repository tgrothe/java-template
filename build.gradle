buildscript {
    apply from: 'dependencies.gradle'
}

plugins {
    id 'java'
    id 'com.diffplug.spotless' version "${spotlessVersion}"
    id 'com.github.ben-manes.versions' version "${benmanesVersion}"
    id 'com.github.spotbugs' version "${spotbugsVersion}"
}

group = 'org.example'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation supportedDependencies.lang3
    implementation 'org.json:json:20240303'
}

tasks.withType(JavaCompile) {
    options.release = 17
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat()
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
}
